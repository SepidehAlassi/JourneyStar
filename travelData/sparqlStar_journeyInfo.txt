PREFIX trip-onto: <http://www.dhlab.unibas.ch/ontology/trip-onto#>
PREFIX schema: <https://schema.org/>
PREFIX : <http://www.dhlab.unibas.ch/data/JBReisebuechlein#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ofn:<http://www.ontotext.com/sparql/functions/>
SELECT ?From ?To ?Departure ?Arrival ?EndDate ?DurationOfStayInDays
WHERE {
    BIND(<http://rdfh.ch/0801/GcXDoaZgRu6E2scC77h6gw> AS ?entryIRI)
    BIND (<< ?person trip-onto:hasJourney ?journey>> AS ?journeyTriple)
    ?journeyTriple trip-onto:mentionedIn ?entryIRI .
   	?journey trip-onto:hasStartLocation ?From .
    BIND(<<	?journey trip-onto:hasStartLocation ?From >> AS ?startStatement)
    ?journey trip-onto:hasDestination ?To .
    ?startStatement trip-onto:hasStartDate ?Departure .
    BIND(<<	?journey trip-onto:hasDestination ?To >> AS ?arrivalStatement)
    ?arrivalStatement trip-onto:hasArrival ?Arrival .
    ?journeyTriple trip-onto:hasEndDate ?EndDate .
    BIND(ofn:days-from-duration(?EndDate-?Arrival) AS ?duration_d)
    BIND(ofn:years-from-duration(?EndDate-?Arrival) AS ?duration_y)
    BIND(ofn:months-from-duration(?EndDate-?Arrival) AS ?duration_m)
    BIND(?duration_y *365+ ?duration_m*12 + ?duration_d AS ?DurationOfStayInDays)
}
